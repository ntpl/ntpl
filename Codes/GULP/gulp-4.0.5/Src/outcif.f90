  subroutine outcif(iout)
!
!  Write out CIF input files
!
!  Current output with out use of symmetry to save output of
!  operators
!
!   3/01 Created based on outxtl
!   7/06 String output modified to handle quotes
!   6/07 lall set to false in calls to setup to avoid
!        potential recursive call issue
!
!  Conditions of use:
!
!  GULP is available free of charge to academic institutions
!  and non-commerical establishments only. Copies should be
!  obtained from the author only and should not be distributed
!  in any form by the user to a third party without the express
!  permission of the author. This notice applies to all parts
!  of the program, except any library routines which are
!  distributed with the code for completeness. All rights for
!  such routines remain with the original distributor.
!
!  No claim is made that this program is free from errors and
!  no liability will be accepted for any loss or damage that
!  may result. The user is responsible for checking the validity
!  of their results.
!
!  Copyright Curtin University 2007
!
!  Julian Gale, NRI, Curtin University, June 2007
!
  use control
  use configurations
  use current
  use element
  use files
  use general
  use symmetry
  implicit none
!
!  Passed variables
!
  integer(i4)        :: iout
!
!  Local variables
!
  character(len=5)   :: lab
  integer(i4)        :: i
  integer(i4)        :: inat
  integer(i4)        :: itype
  integer(i4)        :: nc
!
!  If dumpfile name has been given then open file
!
  if (ciffile(1:1).ne.' ') then
    open(iout,file=ciffile,status='unknown')
  endif
!**********************************************************************
!  Write out data line
!**********************************************************************
  if (names(ncf).ne.' ') then
    write(iout,'(''data_'',a80)')names(ncf)
  else
    write(iout,'(''data_cif'')')
  endif
!
  write(iout,'(/,''_audit_creation_method'',13x,a,/)') "'generated by GULP'"
!*****************************
!  Loop over configurations  *
!*****************************
  do nc = 1,ncfg
    ncf = nc
    if (ndimen(ncf).eq.3) then
      call setup(.false.)
      write(iout,'(''_symmetry_space_group_name_H-M'',5x,a)') "'P 1'"
      write(iout,'(''_symmetry_Int_Tables_number   '',5x,''1'')')
      write(iout,'(''_symmetry_cell_setting        '',5x,''triclinic'',/)')
      write(iout,'(''_cell_length_a'',21x,f9.4)')a
      write(iout,'(''_cell_length_b'',21x,f9.4)')b
      write(iout,'(''_cell_length_c'',21x,f9.4)')c
      write(iout,'(''_cell_angle_alpha'',18x,f8.4)')alpha
      write(iout,'(''_cell_angle_beta'',19x,f8.4)')beta
      write(iout,'(''_cell_angle_gamma'',18x,f8.4)')gamma
      write(iout,'(/,''loop_'')')
      write(iout,'(''_atom_site_label'')')
      write(iout,'(''_atom_site_fract_x'')')
      write(iout,'(''_atom_site_fract_y'')')
      write(iout,'(''_atom_site_fract_z'')')
      write(iout,'(''_atom_site_occupancy'')')
      do i = 1,numat
        if (nat(i).le.maxele) then
          inat = nat(i)
          itype = nftype(i)
          call label(inat,itype,lab)
          write(iout,'(2x,a5,2x,3(f12.5,1x),f7.4)') lab,xfrac(i),yfrac(i),zfrac(i),occuf(i)
        endif
      enddo
    endif
!******************************
!  End of configuration loop  *
!******************************
  enddo
  close(iout)
!
  return
  end
